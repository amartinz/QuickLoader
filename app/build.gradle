/*
 * Copyright 2015 Alexander Martinz
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    signingConfigs {
        release {
            def store = project.findProperty("sign_store") ?: "debug.keystore"
            storeFile file(store)

            if ("debug.keystore".equals(store)) {
                logger.quiet('Not signing the build!')
            } else {
                logger.quiet('Signing the build!')
                storePassword project.property("sign_store_pass")
                keyAlias project.property("sign_key_alias")
                keyPassword project.property("sign_key_pass")
            }
        }
    }

    defaultConfig {
        applicationId "alexander.martinz.quickloader"
        // https://twitter.com/minsdkversion
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 23
        versionName '1.2.0'

        def String gitRevision = getGitCommitHash()
        if (gitRevision != null) {
            versionName(versionName + '-git-' + gitRevision)
        }

        signingConfig signingConfigs.release
    }

    buildTypes {
        release {
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'org.cyanogenmod:platform.sdk:6.0'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'

    // see https://github.com/Evisceration/UniversalDebug
    compile 'at.amartinz:universal-debug:1.0.0'
    compile 'at.amartinz:universal-debug-fabric:1.0.0'

    // see https://github.com/ReactiveX/RxJava
    compile 'io.reactivex:rxjava:1.1.10'
    // see https://github.com/ReactiveX/RxAndroid
    compile 'io.reactivex:rxandroid:1.2.1'
    // see https://github.com/tbruyelle/RxPermissions
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'
}
