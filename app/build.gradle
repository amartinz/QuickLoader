/*
 * Copyright 2015 Alexander Martinz
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    signingConfigs {
        release {
            def String localProps = System.getenv('SIGNING_PROP_FILE')
            if (localProps == null) {
                logger.quiet('Not signing the build!')
                storeFile file("debug.keystore")
            } else {
                logger.quiet('Signing the build!')
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(file(localProps)))
                storeFile file(keyProps["store"])
                storePassword keyProps["storePass"]
                keyAlias keyProps["alias"]
                keyPassword keyProps["pass"]
            }
        }
    }

    defaultConfig {
        applicationId "alexander.martinz.quickloader"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 20
        versionName '1.1.0'

        def String gitRevision = getGitCommitHash()
        if (gitRevision != null) {
            versionName(versionName + '-git-' + gitRevision)
        }

        def String localProps = System.getenv('SIGNING_PROP_FILE')
        if (localProps == null) {
            signingConfig signingConfigs.debug
        } else {
            signingConfig signingConfigs.release
        }
    }

    buildTypes {
        release {
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile 'org.cyanogenmod:platform.sdk:5.0.1'
    compile 'com.android.support:appcompat-v7:24.0.0'

    // see https://github.com/ReactiveX/RxJava
    compile 'io.reactivex:rxjava:1.1.6'
    // see https://github.com/ReactiveX/RxAndroid
    compile 'io.reactivex:rxandroid:1.2.0'
    // see https://github.com/tbruyelle/RxPermissions
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'
}
